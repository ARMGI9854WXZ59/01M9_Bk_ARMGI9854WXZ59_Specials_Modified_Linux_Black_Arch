# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pgspecial
pkgname=('python2-pgspecial' 'python-pgspecial')
_pkgname=pgspecial
pkgver=2.1.1
pkgrel=1
pkgdesc='Meta-commands handler for Postgres Database.'
arch=('any')
url='https://pypi.org/project/pgspecial/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('4ab8596536b90837c2ed0a11b6bbfecd6e5dc487b073a58a1406e8e70028ebb7010430784963fe27a08a5cc48292785f6d02affb750f4a1b74cbe33a0e06a5f0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pgspecial() {
  depends=('python2' 'python2-click' 'python2-sqlparse')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pgspecial() {
  depends=('python' 'python-click' 'python-sqlparse')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

